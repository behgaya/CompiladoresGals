#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
ScannerCaseSensitive = false
ScannerTable = Compact
Input = Stream
Parser = SLR
#RegularDefinitions
id: [a-zA-Z][a-zA-Z0-9_]*

binary: 0b[0-1]+
hexadecimal: 0x[0-9A-Fa-f]+
int: [\+\-]?[0-9]+
float: [\+\-]?[0-9]+[\.][0-9]+
char: [\\'][a-zA-Z0-9][\\']
string: [\"][^\n\r\"]*[\"]

comment: [/][/][^\n\r]*

block_comment: [/][\*]([\s\t\r\n\b]*[^\n\r]*[\s\t\r\n\b]*)*[\*][/]




#Tokens
ID: {id}

KEYWORD_BEGIN = ID : "begin"
KEYWORD_END   = ID : "end"
KEYWORD_IF    = ID : "if"
KEYWORD_THEN  = ID : "then"
KEYWORD_ELSE = ID : "else"
KEYWORD_WHILE = ID : "while"
KEYWORD_DO = ID : "do"
KEYWORD_WRITE = ID : "write"
KEYWORD_INT = ID : "int"
KEYWORD_LONG = ID : "long"
KEYWORD_FLOAT = ID : "float"
KEYWORD_DOUBLE = ID : "double"
KEYWORD_BOOL = ID : "bool"
KEYWORD_CHAR = ID : "char"
KEYWORD_STRING = ID : "string"
KEYWORD_VOID = ID : "voID"
KEYWORD_STATIC = ID : "static"
KEYWORD_FOR = ID : "for"
KEYWORD_CLASS = ID : "class"
KEYWORD_PUBLIC = ID: "public"
KEYWORD_PRIVATE = ID : "private"
KEYWORD_PRINT = ID : "print"
KEYWORD_READ = ID : "read"
KEYWORD_CALL = ID : "call"
KEYWORD_RETURN = ID : "return"
KEYWORD_TRUE = ID : "true"
KEYWORD_FALSE = ID : "false"
ADDITION: "+"
SUBTRACTION: "-"
MULTIPLICATION: "*"
DIVISION: "/"
EQUALS: "="
MODULO: "%"
PLUS_PLUS: "++"
MINUS_MINUS: "--"


PLUS_EQUALS: "+="
MINUS_EQUALS: "-="
TIMES_EQUALS: "*="
DIVIDE_EQUALS: "/="

BIGGER_THAN: ">"
BIGGER_EQUAL: ">="
LESS_THAN: "<"
LESS_EQUAL: "<="
EQUAL_TO: "=="
NOT_EQUAL: "!="

AND: "&&"
OR: "||"
NOT: "!"

BITWISE_AND: "&"
BITWISE_OR: "|"
BITWISE_XOR: "^"
BITWISE_NOT: "~"
LEFT_SHIFT: "<<"
RIGHT_SHIFT: ">>"

BINARY_NUMBER: {binary}
HEXADECIMAL_NUMBER: {hexadecimal}
INTEGER_NUMBER: {int}
FLOAT_NUMBER: {float}
CHAR_VALUE: {char}
STRING_VALUE: {string}

COMMENTARY: {comment}

BLOCK_COMMENTARY: {block_comment}

SEMICOLON: ";"
COLON: ":"
COMMA: ","
PERIOD: "."
CLOSE_BRACES: "}"
OPEN_BRACES: "{"
CLOSE_BRACKETS: "]"
OPEN_BRACKETS: "["
CLOSE_PARENTHESES: ")"
OPEN_PARENTHESES: "("
HASH_SIGN: "#"


:[\s\t\r\n\b]*
#NonTerminals
<programa>
<lista_rotina>
<funcao>
<procedimento>
<rotina>
<tipo>
<variavel>
<parenteses_funcao>
<bloco>
<id>
<declaracao_funcao>
<lista_cmd>
<cmd>
<declaracao>
<declaracao1>
<repeticao_while>
<chamar_funcao>
<desvio_if>
<print>
<return>
<lista_comentarios>
<comment>
<block_comment>
<read_line>
<exp>
<parametros_chamar>
<declaracao2>
<atribuicao>
<declaracao3>
<declaracao4>
<op_atribuicao>
<vetor>
<repeticao_for>
<declaracao_controle>
<operacao_incremento>
<op_incremento>
<repeticao_do>
<incremento>
<void>
<desvio_else>
<read>
<bool>
<exp1>
<exp2>
<exp3>
<exp4>
<exp5>
<op_rel>
<exp6>
<op_bitwise>
<exp7>
<op_arit_baixa>
<exp8>
<op_arit_alta>
<exp9>
<op_neg>
<exp10>
#Grammar
<programa> ::= <lista_rotina>;


<lista_rotina> ::= <rotina> | <lista_rotina> <rotina>;
<rotina> ::= <funcao> | <procedimento> | <lista_comentarios>; 


<funcao> ::= <tipo> <id>#6  OPEN_PARENTHESES <parenteses_funcao> CLOSE_PARENTHESES#27 <bloco>#2 | <tipo> <id>#6 OPEN_PARENTHESES CLOSE_PARENTHESES#27 <bloco>#2;
<procedimento> ::= <void>#7 <id> OPEN_PARENTHESES <parenteses_funcao> CLOSE_PARENTHESES <bloco> | <void>#7 <id> OPEN_PARENTHESES CLOSE_PARENTHESES <bloco>;
<parenteses_funcao> ::= <declaracao>#26 | <declaracao>#26 COMMA <parenteses_funcao>;


<bloco> ::= OPEN_BRACES#1 CLOSE_BRACES#2 | OPEN_BRACES#1 <lista_cmd> CLOSE_BRACES#2;
<lista_cmd> ::= <cmd> | <lista_cmd> <cmd>;
<cmd> ::= <declaracao1> SEMICOLON | <repeticao_while> | <chamar_funcao> SEMICOLON | <desvio_if> | <print> SEMICOLON | 
	<return> SEMICOLON | <comment> | <block_comment> | <read_line> SEMICOLON | <atribuicao> SEMICOLON | <incremento> SEMICOLON ;


<return> ::= KEYWORD_RETURN <exp>#24;


<chamar_funcao> ::= <id>#25 OPEN_PARENTHESES#28 CLOSE_PARENTHESES#29 | <id>#25 OPEN_PARENTHESES#28 <parametros_chamar> CLOSE_PARENTHESES#29;
<parametros_chamar> ::= <exp>#24 | <parametros_chamar> COMMA <exp>#24;


<lista_comentarios> ::= <comment> | <block_comment>;
<comment> ::= COMMENTARY;
<block_comment> ::= BLOCK_COMMENTARY;


<declaracao> ::= <tipo>#8 <variavel>#9;
<declaracao1> ::= <tipo>#8 <variavel>#9 | <declaracao2>;
<declaracao2> ::= <tipo>#8 <atribuicao> | <declaracao3>;
<declaracao3> ::= <declaracao1> COMMA <variavel>#9 | <declaracao4>;
<declaracao4> ::= <declaracao1> COMMA <atribuicao>;


<atribuicao> ::= <variavel>#10 <op_atribuicao> <exp>#11;
<variavel> ::= <id> | <vetor>;
<vetor> ::= <id>#5 OPEN_BRACKETS CLOSE_BRACKETS | <id>#5 OPEN_BRACKETS <exp> CLOSE_BRACKETS;


<repeticao_while> ::= KEYWORD_WHILE#1 <exp>#24 <bloco>#2 | <repeticao_for>;
<repeticao_for> ::= KEYWORD_FOR#30 OPEN_PARENTHESES <declaracao_controle> SEMICOLON <exp>#24 SEMICOLON <operacao_incremento> CLOSE_PARENTHESES <bloco>#2;
<declaracao_controle> ::= <declaracao1> | <atribuicao> | <declaracao1> <atribuicao>;
<operacao_incremento> ::= <atribuicao> | <incremento>;
<repeticao_do> ::= KEYWORD_DO#1 <bloco>#2 KEYWORD_WHILE <exp>#24;


<incremento> ::= <id>#12 <op_incremento>;

<desvio_if> ::= KEYWORD_IF#1 <exp>#24 <bloco>#2 | KEYWORD_IF#1 <exp>#24 <bloco>#2 <desvio_else>;
<desvio_else> ::= KEYWORD_ELSE <bloco>;


<print> ::= KEYWORD_PRINT <exp>#24;
<read_line> ::= <declaracao1> COLON <read>;
<read> ::= KEYWORD_READ OPEN_PARENTHESES CLOSE_PARENTHESES;


<tipo> ::= KEYWORD_INT#3 | KEYWORD_FLOAT#3 | KEYWORD_CHAR#3 | KEYWORD_BOOL#3 | KEYWORD_STRING#3 | KEYWORD_CLASS#3;
<void> ::= KEYWORD_VOID#3;
<bool> ::= KEYWORD_TRUE | KEYWORD_FALSE;
<id> ::= ID#4;


<exp> ::= <exp> OR#19 <exp1> | <exp1>;
<exp1> ::= <exp1> AND#19 <exp2> | <exp2>;
<exp2> ::= <exp2> BITWISE_OR#19 <exp3> | <exp3>;
<exp3> ::= <exp3> BITWISE_XOR#19 <exp4> | <exp4>;
<exp4> ::= <exp4> BITWISE_AND#19 <exp5> | <exp5>;
<exp5> ::= <exp5> <op_rel>#19 <exp6> | <exp6>;
<exp6> ::= <exp6> <op_bitwise>#19 <exp7> | <exp7>;
<exp7> ::= <exp7> <op_arit_baixa> <exp8> | <exp8>;
<exp8> ::= <exp8> <op_arit_alta> <exp9> | <exp9>;
<exp9> ::= <op_neg>#19 <exp10> | <exp10>;
<exp10> ::= STRING_VALUE#14 | INTEGER_NUMBER#15 | FLOAT_NUMBER#16 | <bool>#17 | CHAR_VALUE#18 | OPEN_PARENTHESES <exp> CLOSE_PARENTHESES | <id>#13 | <vetor> | <chamar_funcao>;


<op_rel> ::= BIGGER_THAN | LESS_THAN | BIGGER_EQUAL | LESS_EQUAL | EQUALS | NOT_EQUAL | EQUAL_TO;
<op_neg> ::= SUBTRACTION | BITWISE_NOT | NOT;
<op_bitwise> ::= LEFT_SHIFT | RIGHT_SHIFT;
<op_arit_baixa> ::= ADDITION#20 | SUBTRACTION#21;
<op_arit_alta> ::= MULTIPLICATION#22 | DIVISION#23 | MODULO#23;
<op_incremento> ::= PLUS_PLUS | MINUS_MINUS;
<op_atribuicao> ::= EQUALS | PLUS_EQUALS | MINUS_EQUALS | TIMES_EQUALS | DIVIDE_EQUALS;





